apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.6
targetCompatibility = 1.6

version = '5.0.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.retrofit:converter-jackson:1.9.0'

    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'

    compile 'io.reactivex:rxjava:1.0.0'
    compile 'org.projectlombok:lombok:1.16.8'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.11'
    testCompile group: 'com.squareup.okhttp', name: 'mockwebserver', version: '2.0.0'
    testCompile 'org.awaitility:awaitility-scala:2.0.0'

    task fatJar(type: Jar) {
        baseName = project.name + '-all'
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }
}

apply plugin: 'maven-publish'

def pomConfig = {
    description 'Voucherify-java-sdk is a Java client (can be used in Android application as well) which was created to simplify integration with Voucherify backend (http://www.voucherify.io)'
    url 'https://github.com/rspective/voucherify-java-sdk'
    scm {
        url 'https://github.com/rspective/voucherify-java-sdk.git'
        connection 'scm:git@github.com:rspective/voucherify-java-sdk.git'
        developerConnection 'scm:git@github.com:rspective/voucherify-java-sdk.git'
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'rspective'
            name 'RSPECTIVE P RYCHLIK SPOLKA JAWNA'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.voucherify.client'
            artifactId 'voucherify-java-sdk'

            from components.java

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
}